
aspectdef findVariablesInJoinpoint
    input jp end
    output list end 

    list = [];
    select jp.vardecl end
    apply
        list.push($vardecl.name);
    end
end

aspectdef getJoinpointsToChange
    input jp, latvars end
    output latvarsOut end 

    latvarsOut = latvars;

    //find vardecls
    select jp.vardecl end
    apply
        for(var i = 0; i < latvarsOut.length; i++){
            if(latvarsOut[i].name == $vardecl.name){
                latvarsOut[i].vardecl = $vardecl;
                break;
            }
        }
    end
    for(var i = 0; i < latvarsOut.length; i++){
        latvarsOut[i].varrefs = [];
    }
    //find varrefs
    select jp.varref end
    apply
        for(var i = 0; i < latvarsOut.length; i++){
            if(latvarsOut[i].name == $varref.name){
                latvarsOut[i].varrefs.push($varref);
                println("Added " + $varref.name + " : " + $varref.code);
                break;
            }
        }
    end
end

aspectdef getJoinpointsToChangeForSingleLatVar
    input jp, latvar end
    output latvarOut end 

    latvarOut = latvar;

    //find vardecls
    select jp.vardecl end
    apply
        if(latvarOut.name == $vardecl.name){
            latvarOut.vardecl = $vardecl;
            break;
        }
    end

    //find varrefs
    latvarOut.varrefs = [];
    select jp.varref end
    apply
        if(latvarOut.name == $varref.name){
            latvarOut.varrefs.push($varref);
        }
    end
end
aspectdef getJoinpointsToChangeForLatVarOmp
    input latvar end
    output latvarOut end 

    latvarOut = latvar;
    latvarOut.pragma = undefined;
    //find pragma with the same name as the LatVarOmp passed
    select pragma end
    apply
        if(latvarOut.name == $pragma.name){
            latvarOut.pragma = $pragma;
            break;
        }
    end
    

end

aspectdef getFile
    output thisFile end 
    
    select file end
    apply
        thisFile = $file;
    end

end


/*aspectdef printCode
    select program end
    apply
        println("AST:\n" + $program.code);
    end
end*/